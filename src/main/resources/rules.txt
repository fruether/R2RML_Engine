@prefix rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs:   <http://www.w3.org/2000/01/rdf-schema> .
@prefix sl: <http://softlang.com/> .
@prefix maven: <http://softlang.com/plugins/maven/> .
@prefix technologies: <http://softlang.com/Plugins/> .
@prefix technologiesSmall: <http://softlang.com/plugins/> .


[XSDCheck:
    (?a sl:elementOf sl:XML)
    (?b sl:elementOf sl:XSD)
    XSDCheck(?a,?b) ->
    (?a sl:conformsTo ?b)]

[DetectProjectDependencies:
    (?file sl:conformsTo maven:maven-4.0.0.xsd)
    (?file sl:partOf ?build)
    (?file  sl:references ?dependency) ->
    (?build sl:dependsOn ?dependency)]

[DetectLiquidBase:
    (?file sl:conformsTo maven:maven-4.0.0.xsd)
    liquidBaseDependencyCheck(?file) ->
    (?file sl:references technologies:liquidbase)]

[LanguageDetection:
    (?file sl:partOf ?build)
    (?file rdf:type sl:Artefact)
    Parse(?file, ?language)
    ->
    (?file sl:elementOf ?language)
]
[JavaClassIdentification:
    (?file sl:partOf ?build)
    (?file rdf:type sl:Artefact)
    (?file sl:elementOf sl:Java)
    RetrieveClass(?file, ?classname)
    ->
    (?file sl:hasClass ?classname)
]

[DTDDetection:
    (?file sl:partOf ?build)
    (?file rdf:type sl:Artefact)
    FileEnding(?file, ".dtd")
    ->
    (?file sl:elementOf sl:DTD)
]
[DTDValidation:
    (?file1 sl:partOf ?build1)
    (?file1 rdf:type sl:Artefact)
    (?file1 sl:elementOf sl:XML)
    (?file2 sl:partOf ?build2)
    (?file2 rdf:type sl:Artefact)
    (?file2 sl:elementOf sl:DTD)
    DTDCheck(?file1, ?file2)
    ->
    (?file1 sl:conformsTo ?file2)
]
[RetrieveReferences:
     (?file1 sl:partOf ?build1)
     (?file1 rdf:type sl:Artefact)
     (?file2 sl:partOf ?build1)
     (?file2 rdf:type sl:Artefact)
     CheckReference(?file1, ?file2)
     ->
     (?file1 sl:references ?file2)
 ]

[HibernateDependency:
    (?file sl:conformsTo maven:maven-4.0.0.xsd)
    HibernateDependencyCheck(?file) ->
    (?file sl:references technologies:hibernate)
]
[HibernateRoles:
    (?file rdf:type sl:Artefact)
    (?file sl:partOf ?build1)
    (?file sl:elementOf sl:XML)
    (?file sl:conformsTo ?schema)
    (?schema sl:partOf sl:hibernate)
    HibernateRoleIdentification(?schema, ?role)
    ->
    (?file sl:hasRole ?role)
]
[HibernateReferences:
    (?file1 rdf:type sl:Artefact)
    (?file1 sl:hasRole technologies:Hibernate/HibernateConfiguration)
    (?file2 rdf:type sl:Artefact)
    (?file2 sl:hasRole technologies:Hibernate/HibernateMapping)
    CheckReference(?file1, ?file2)
    ->
    (?file1 sl:HibernateConfigurationRefersToHibernateMapping ?file2)
]
[HibernateMapping:
    (?file1 rdf:type sl:Artefact)
    (?file1 sl:hasRole technologies:Hibernate/HibernateMapping)
    (?file2 rdf:type sl:Artefact)
    (?file2 sl:elementOf sl:Java)
    (?file2 sl:hasClass ?class)
    HibernateMappingAnalysis(?file1, ?class)
    ->
    (?file1 sl:refersTo ?class)
]
[HibernateObjectDefinition:
    (?file1 rdf:type sl:Artefact)
    (?file1 sl:hasRole technologies:Hibernate/HibernateMapping)
    (?file2 rdf:type sl:Artefact)
    (?file2 sl:elementOf sl:Java)
    (?file2 sl:hasClass ?class)
    (?file1 sl:refersTo ?class)
    GetClassLiteral(?class, ?classLanguage)
    ->
    (?class sl:defines ?classLanguage)
    (?classLanguage rdf:type sl:Language)
    (?classLanguage sl:subsetOf sl:JVMObject)
]

[HibernateMappingFunction:
    (?file rdf:type sl:Artefact)
    (?file sl:hasRole technologies:Hibernate/HibernateMapping)
    HibernateGetMappingType(?file, ?function)
    ->
    (?file sl:defines ?function)
    (?function rdf:type sl:Function)
    (?function sl:partOf sl:OR-Mapping)
]
[HibernateMappingConf:
    (?file rdf:type sl:Artefact)
    (?file sl:hasRole technologies:Hibernate/HibernateConfiguration)
    ->
    (?file sl:defines sl:OR-Mapping)
]
[HibernateMappingFunctionDefinition:
    (?mappingFile rdf:type sl:Artefact)
    (?mappingFile sl:elementOf sl:XML)
    (?mappingFile sl:defines ?function)
    (?function sl:partOf sl:OR-Mapping)
    (?mappingFile sl:refersTo ?className)
    (?className sl:defines ?language)
    ->
    (?language sl:input ?function)
]

[PackageDetection:
    (?file rdf:type sl:Artefact)
    (?file sl:elementOf sl:Java)
    (?file sl:hasClass ?class)
    (?package rdf:type sl:Package)
    CheckImport(?file, ?package)
    ->
    (?file sl:uses ?package)
]
[ClassDetection:
    (?file1 rdf:type sl:Artefact)
    (?file1 sl:elementOf sl:Java)
    (?file1 sl:hasClass ?class1)
    (?file1 sl:partOf ?build)
    (?file2 rdf:type sl:Artefact)
    (?file2 sl:elementOf sl:Java)
    (?file2 sl:hasClass ?class2)
    (?file2 sl:partOf ?build)
    notEqual(?file1, ?file2)
    CheckClassReference(?file1, ?class2)
    ->
    (?class1 sl:uses ?class2)
]
[MethodCallDetection:
    (?file rdf:type sl:Artefact)
    (?file sl:elementOf sl:Java)
    (?file sl:hasClass ?class)
    (?package rdf:type sl:Package)
    (?file sl:uses ?package)
    (?method sl:partOf ?package)
    (?method rdf:type sl:Method)
    CheckCall(?file, ?method, ?class)
    ->
    (?class sl:calls ?method)
]
[MethodCallDetectionWithDependency:
    (?file rdf:type sl:Artefact)
    (?file sl:elementOf sl:Java)
    (?file sl:hasClass ?class)
    (?package rdf:type sl:Package)
    (?packageDep rdf:type sl:Package)
    (?package sl:uses ?packageDep)
    (?file sl:uses ?package)
    (?method sl:partOf ?packageDep)
    (?method rdf:type sl:Method)
    CheckCall(?file, ?method, ?class)
    ->
    (?class sl:calls ?method)
]

[HibernateORMappingDetection:
    (?file rdf:type sl:Artefact)
    (?file sl:elementOf sl:Java)
    (?file sl:hasClass ?theClass)
    (?theClass sl:uses ?class)
    (?mappingFile sl:refersTo ?class)
    (?mappingFile sl:defines ?function)
    (?mappingFile sl:hasRole technologies:Hibernate/HibernateMapping)
    (?theClass sl:calls ?method)
    (?method sl:partOf technologiesSmall:hibernate/Package/org.hibernate.Session)
 ->
    (?theClass sl:uses ?function)


]
